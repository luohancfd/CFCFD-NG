# plot_data.gnu

# moles immediately post shock
C_ps = 0.409194171723

# moles at equilibrium
C_eq = 0.676766316384

C_ratio = C_eq/C_ps

# from CEA2 calculation
# moles/original moles at equilibrium
e = 2.2169e-5*C_ratio
N = 1.6496e-2*C_ratio
NO = 1.3614e-2*C_ratio
N2 = 6.5455e-1*C_ratio
O = 3.1353e-1*C_ratio
O2 = 1.7606e-3*C_ratio

T_ratio = 15.160
rho_ratio = 10.5723

set arrow 1 from 25,T_ratio to 45,T_ratio nohead
set arrow 2 from 25,rho_ratio to 45,rho_ratio nohead
set arrow 3 from 33,13.5 to 33,T_ratio
set arrow 4 from 33,12.5 to 33,rho_ratio
set label 3 "CEA2 calculation" at 2.5,13
set term postscript eps enhanced color 20
set output "profile_T_rho.eps"
set title "Temperature and density distribution behind a normal shock\nM_{/Symbol \245}=12.28, T_{/Symbol \245}=300.0 K, p_{/Symbol \245}=133.32 Pa"
set xlabel "distance behind shock (cm)"
set ylabel "temperature, density ratio"
set xrange [0.01:100]
set yrange [6:24]
set logscale x
set mytics 2
set grid xtics ytics mxtics mytics
set label 1 "T/T_{{/Symbol \245}}" at 0.2,20
set label 2 "{/Symbol r}/{/Symbol r}_{{/Symbol \245}}" at 0.2,8
plot "air-Mach-12.3.data" u ($1*100):($2/300.0) t 'present work' w l ls 1, \
     "marrone_fig4_T_ratio.g3data" u 1:2 t 'Marrone 1963' w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($4/1.539e-03) notitle w l ls 1, \
     "marrone_fig4_rho_ratio.g3data" u 1:2 notitle w l ls 3

unset label 1
unset label 2
unset label 3
unset arrow 1
unset arrow 2

set arrow 1 from 25,N2 to 45,N2 nohead
set label 1 "N2" at 50,N2
set arrow 2 from 25,O2 to 45,O2 nohead
set label 2 "O2" at 50,O2
set arrow 3 from 25,NO to 45,NO nohead
set label 3 "NO" at 50,NO-0.5e-2
set arrow 4 from 25,e to 45,e nohead
set label 4 "NO+,e-" at 30,e-1.5e-5
set arrow 5 from 25,N to 45,N nohead
set label 5 "N" at 50,N+0.02
set arrow 6 from 25,O to 45,O nohead
set label 6 "O" at 50,O

set output "profile_moles.eps"
set title "Species concentrations behind a normal shock in air\nM_{/Symbol \245}=12.28, T_{/Symbol \245}=300.0 K, p_{/Symbol \245}=133.32 Pa"
set xlabel "distance behind shock (cm)"
set ylabel "moles per original mole"
set logscale y
set mytics 10
set yrange [1.0e-7:2]
set xrange [0.01:100]
set grid nomxtics nomytics
set format y "10^{%L}"
set key bottom right
plot "marrone_fig3_N2.g3data" using 1:2 title 'Marrone 1963' w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($6/C_ps)  title 'present work' w l ls 1, \
     "marrone_fig3_O2.g3data" using 1:2 notitle w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($7/C_ps) notitle w l ls 1, \
     "marrone_fig3_NO.g3data" using 1:2 notitle w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($8/C_ps) notitle w l ls 1, \
     "marrone_fig3_O.g3data" using 1:2 notitle w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($9/C_ps) notitle w l ls 1, \
     "marrone_fig3_N.g3data" using 1:2 notitle w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($10/C_ps) notitle w l ls 1, \
     "marrone_fig3_NO+_e.g3data" using 1:2 notitle w l ls 3, \
     "air-Mach-12.3.data" u ($1*100):($11/C_ps) notitle w l ls 1, \
     "air-Mach-12.3.data" u ($1*100):($12/C_ps) notitle w l ls 1


