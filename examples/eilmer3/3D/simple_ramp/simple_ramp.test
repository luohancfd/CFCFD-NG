# simple_ramp.test
# Functional test for a 3D, essentially-inviscid flow.
#
# This exercises quite a few of the basic functions of the 3D code.
#
# PJ, 14-Jan-2011
#     22-Feb-2014 Update expected values for interpolate-in-local-frame.

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test run-preprocessor {Run the preprocessing stage.} -body {
    exec e3prep.py --job=simple_ramp > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the shared-memory simulation code.} -body {
    exec e3shared.exe --job=simple_ramp --run > LOGFILE_MAIN
} -result {} -returnCodes {0}

test number-of-steps {The main simulation takes a certain number of steps.} -body {
    set fp [open LOGFILE_MAIN r]
    set contents [read $fp]
    close $fp
    set final_steps 0
    foreach line [split $contents "\n"] {
	if {[string first "Total number of steps" $line] >= 0} {
	    set final_steps [lindex [split $line] 5]
	}
    }
    set final_steps
} -result {862}

test shock-angle {The shock angle should be close to 57 degrees.} -body {
    set shock_angle 0
    set average_deviation 0
    if { [catch {exec /usr/bin/env python estimate_shock_angle.py} results] } {
        puts "specialized postprocessing command failed"
        puts $results
    } else {
	foreach line [split $results "\n"] {
	    if {[string first shock_angle_deg $line] >= 0} {
		set shock_angle [lindex [split $line] 1]
	    }
	    if {[string first average_deviation_metres $line] >= 0} {
		set average_deviation [lindex [split $line] 1]
	    }
	}
    }
    # The shock wave angle should be within 1 degree of expected
    # and average deviation of points should be less than 2mm. 
    list [expr abs($shock_angle - 57) < 1.0] [expr $average_deviation < 0.002]
} -result {1 1}

test estimated-forces {The forces are approximately 2210,0,-12540 Newtons.} -body {
    set f_x dont_know
    set f_y dont_know
    set f_z dont_know
    if { [catch {exec /usr/bin/env python estimate_ramp_force.py} results] } {
        puts "specialized postprocessing command failed"
        puts $results
    } else {
	foreach line [split $results "\n"] {
	    if {[string first force= $line] >= 0} {
		scan $line "force= Vector3(%f,%f,%f) Newtons" f_x f_y f_z
	    }
	}
    }
    # The shock wave angle should be within 1 degree of expected
    # and average deviation of points should be less than 2mm. 
    list [expr abs($f_x - 2215)/2215 < 0.003] [expr abs($f_y) < 0.001] \
	[expr abs($f_z + 12560)/12560 < 0.002]
} -result {1 1 1}

cleanupTests
