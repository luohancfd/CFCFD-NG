/* analytic_solution.mac
 *
 * Maxima expressions for the analytic solution described in Appendix A of
 * C.J. Roy, C.C. Nelson, T.M. Smith and C.C. Ober
 * Verification of Euler/Navier-Stokes codes using the method of 
 * manufactured solutions.
 * Int J for Numerical Methods in Fluids 2004; 44:599-620
 *
 * PJ, 28-May-2011
 *     30-Jun-2012 Scale to reduce the perturbation away from the centre.
 *     07-Nov-2012 yzx version
 */

L:1.0;

if case = 1 then
   block(rho0:1.0, rhoy:0.15, rhoz:-0.1, rhoyz:0.0, arhoy:1.0, arhoz:0.5, arhoyz:0.0,
         u0:800.0, uy:50.0, uz:-30.0, uyz:0.0, auy:1.5, auz:0.6, auyz:0.0,
         v0:800.0, vy:-75.0, vz:40.0, vyz:0.0, avy:0.5, avz:2.0/3, avyz:0.0,
         p0:1.0e5, py:0.2e5, pz:0.5e5, pyz:0.0, apy:2.0, apz:1.0, apyz:0.0)
else
   block(rho0:1.0, rhoy:0.1, rhoz:0.15, rhoyz:0.08, arhoy:0.75, arhoz:1.0, arhoyz:1.25,
         u0:70.0, uy:4.0, uz:-12.0, uyz:7.0, auy:5.0/3, auz:1.5, auyz:0.6,
         v0:90.0, vy:-20.0, vz:4.0, vyz:-11.0, avy:1.5, avz:1.0, avyz:0.9,
         p0:1.0e5, py:-0.3e5, pz:0.2e5, pyz:-0.25e5, apy:1.0, apz:1.25, apyz:0.75);

rsq: (y-L/2)^2 + (z-L/2)^2;
if case = 1 or case = 2 then
   S: 1.0
else
   S: exp(-16.0*rsq/(L^2));
   
rho: rho0 + S*rhoy*sin(arhoy*%pi*y/L) + S*rhoz*cos(arhoz*%pi*z/L) +
   S*rhoyz*cos(arhoyz*%pi*y*z/(L*L));
u: u0 + S*uy*sin(auy*%pi*y/L) + S*uz*cos(auz*%pi*z/L) + S*uyz*cos(auyz*%pi*y*z/(L*L));
v: v0 + S*vy*cos(avy*%pi*y/L) + S*vz*sin(avz*%pi*z/L) + S*vyz*cos(avyz*%pi*y*z/(L*L));
p: p0 + S*py*cos(apy*%pi*y/L) + S*pz*sin(apz*%pi*z/L) + S*pyz*sin(apyz*%pi*y*z/(L*L));
