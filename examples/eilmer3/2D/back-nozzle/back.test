# back.test
#
# This exercises the SubsonicInBC that forms the upstream boundary for
# the simulation of the Back et al. nozzle.
#
# PJ, 03-Mar-2014

package require tcltest 2.0
namespace import ::tcltest::*
configure -verbose {start body error}

test run-preprocessor {Run the preprocessing stage.} -body {
    exec e3prep.py --job=back --do-svg > LOGFILE_PREP
} -result {} -returnCodes {0}

test run-simulation {Run the parallel simulation code.} -body {
    exec mpirun -np 2 e3mpi.exe --job=back --run > LOGFILE_MAIN
} -result {} -returnCodes {0}

test number-of-steps {Iteration of the final block in a certain number of steps.} -body {
    set fp [open LOGFILE_MAIN r]
    set contents [read $fp]
    close $fp
    set final_steps 0
    foreach line [split $contents "\n"] {
	if {[string first "Total number of steps" $line] >= 0} {
	    set final_steps [lindex [split $line] 5]
	}
    }
    expr abs($final_steps - 5408) < 3
} -result {1}

test subsonic-region {Subsonic region should have a small positive Mach number.} -body {
    # The following values were extracted from the solution
    # as it was computed on PJ's computer 03-Mar-2014.
    set rho_ref 5.743503e+00
    set p_ref 4.927381e+05
    set T_ref 2.988554e+02
    set Mach_ref 1.384033e-01
    # Probe the solution near the centreline of the upstream boundary
    # and pull a few values from the line of data that is produced by e3post.
    if { [catch {exec e3post.py --job=back --tindx=last --probe=-0.075,0.0,0.0 --add-mach} results] } {
        puts "specialized postprocessing command failed"
        puts $results
	set rho 0
	set p 0
	set T 0
	set Mach 0
    } else {
	set line_of_data [string trim [lindex [split $results "\n"] end]]
	# puts $line_of_data
	set items [split $line_of_data]
	set rho [lindex $items 4]; # remember that index starts at 0
	set p [lindex $items 8]
	set T [lindex $items 19]
	set Mach [lindex $items 20]
	# puts "rho=$rho p=$p T=$T Mach=$Mach"
    }
    # The differences with respect to the reference values should be small. 
    list [expr abs($rho - $rho_ref)/$rho_ref < 1.0e-3] \
    	 [expr abs($p - $p_ref)/$p_ref < 1.0e-3] \
    	 [expr abs($T - $T_ref)/$T_ref < 1.0e-3] \
    	 [expr abs($Mach - $Mach_ref)/$Mach_ref < 1.0e-3]
} -result {1 1 1 1}

test supersonic-region {Supersonic exit should be a bit over Mach 3.} -body {
    # The following values were extracted from the solution
    # as it was computed on PJ's computer 03-Mar-2014.
    set rho_ref 3.904117e-01
    set p_ref 1.142634e+04
    set T_ref 1.019545e+02
    set Mach_ref 3.116296e+00
    # Probe the solution near the centre of the nozzle exit
    # and pull a few values from the line of data that is produced by e3post.
    if { [catch {exec e3post.py --job=back --tindx=last --probe=0.075,0.0,0.0 --add-mach} results] } {
        puts "specialized postprocessing command failed"
        puts $results
	set rho 0
	set p 0
	set T 0
	set Mach 0
    } else {
	set line_of_data [string trim [lindex [split $results "\n"] end]]
	# puts $line_of_data
	set items [split $line_of_data]
	set rho [lindex $items 4]; # remember that index starts at 0
	set p [lindex $items 8]
	set T [lindex $items 19]
	set Mach [lindex $items 20]
	# puts "rho=$rho p=$p T=$T Mach=$Mach"
    }
    # The differences with respect to the reference values should be small. 
    list [expr abs($rho - $rho_ref)/$rho_ref < 1.0e-3] \
    	 [expr abs($p - $p_ref)/$p_ref < 1.0e-3] \
    	 [expr abs($T - $T_ref)/$T_ref < 1.0e-3] \
    	 [expr abs($Mach - $Mach_ref)/$Mach_ref < 1.0e-3]
} -result {1 1 1 1}

cleanupTests
