/* analytic_solution.mac
 *
 * Maxima expressions for the analytic solution described in Appendix A of
 * C.J. Roy, C.C. Nelson, T.M. Smith and C.C. Ober
 * Verification of Euler/Navier-Stokes codes using the method of 
 * manufactured solutions.
 * Int J for Numerical Methods in Fluids 2004; 44:599-620
 *
 * PJ, 28-May-2011
 */

L:1.0;

if case = 1 then
   block(rho0:1.0, rhox:0.15, rhoy:-0.1, rhoxy:0.0, arhox:1.0, arhoy:0.5, arhoxy:0.0,
         u0:800.0, ux:50.0, uy:-30.0, uxy:0.0, aux:1.5, auy:0.6, auxy:0.0,
         v0:800.0, vx:-75.0, vy:40.0, vxy:0.0, avx:0.5, avy:2.0/3, avxy:0.0,
         p0:1.0e5, px:0.2e5, py:0.5e5, pxy:0.0, apx:2.0, apy:1.0, apxy:0.0)
else
   block(rho0:1.0, rhox:0.1, rhoy:0.15, rhoxy:0.08, arhox:0.75, arhoy:1.0, arhoxy:1.25,
         u0:70.0, ux:4.0, uy:-12.0, uxy:7.0, aux:5.0/3, auy:1.5, auxy:0.6,
         v0:90.0, vx:-20.0, vy:4.0, vxy:-11.0, avx:1.5, avy:1.0, avxy:0.9,
         p0:1.0e5, px:-0.3e5, py:0.2e5, pxy:-0.25e5, apx:1.0, apy:1.25, apxy:0.75);

rho: rho0 + rhox*sin(arhox*%pi*x/L) + rhoy*cos(arhoy*%pi*y/L) +
   rhoxy*cos(arhoxy*%pi*x*y/(L*L));
u: u0 + ux*sin(aux*%pi*x/L) + uy*cos(auy*%pi*y/L) + uxy*cos(auxy*%pi*x*y/(L*L));
v: v0 + vx*cos(avx*%pi*x/L) + vy*sin(avy*%pi*y/L) + vxy*cos(avxy*%pi*x*y/(L*L));
p: p0 + px*cos(apx*%pi*x/L) + py*sin(apy*%pi*y/L) + pxy*sin(apxy*%pi*x*y/(L*L));
